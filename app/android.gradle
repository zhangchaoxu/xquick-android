// load local.properties
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 28

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize "8g" //specify the heap size for the dex process
        preDexLibraries = false //delete the already pre-dexed libraries
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId properties.getProperty("packageName")
        minSdkVersion 19
        targetSdkVersion 28
        versionCode Integer.valueOf(properties.getProperty("versionCode"))
        versionName properties.getProperty("versionName")

        multiDexEnabled true

        manifestPlaceholders = [
                PACKAGE_NAME: properties.getProperty("packageName")
        ]

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["moduleName": project.name]
            }
        }

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }

    // sign the application
    signingConfigs {
        release {
            storeFile file(properties.getProperty("releaseStoreFile"))
            storePassword properties.getProperty("releaseStorePassword")
            keyAlias properties.getProperty("releaseKeyAlias")
            keyPassword properties.getProperty("releaseKeyPassword")
        }
        debug {
            storeFile file(properties.getProperty("debugStoreFile"))
            storePassword properties.getProperty("debugStorePassword")
            keyAlias properties.getProperty("debugKeyAlias")
            keyPassword properties.getProperty("debugKeyPassword")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}
